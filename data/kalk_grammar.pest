document = {soi ~ (statement ~ (newline+ | &eoi))* ~ eoi}

statement = {assignment}
assignment = {identifier ~ &":=" ~ operator ~ expr}

expr = {
    math_expr
    | atomic_expr
}

atomic_expr = _{"(" ~ expr ~ ")" | identifier | literal}
math_expr = _{sum | prod | negate | exp}

sum = {(prod | negate | exp | atomic_expr) ~ (&("+" | "-") ~ operator ~ (prod | exp | atomic_expr))+}
prod = {
    (negate | exp | atomic_expr) ~ (
        &("*" | "/") ~ operator ~ (negate | exp | atomic_expr)
        | (exp | atomic_expr)
    )+
}
negate = {"-" ~ (exp | atomic_expr)}
exp = {atomic_expr ~ (&"^" ~ operator ~ (negate | exp | atomic_expr))+}

operator = {":=" | "=" | "<" | ">" | "<=" | ">=" | "!=" | "!" | "&&" | "||" | "+" | "-" | "*" | "/" | "^"}
identifier = @{alpha ~ (alpha | digit | "_")*}
literal = {number}
number = @{digit+ ~ ("." ~ digit+)?}

digit = _{'0'..'9'}
alpha = _{'a'..'z' | 'A'..'Z'}
comment = _{"--" ~ (!newline ~ any)*}
newline = _{"\r\n" | "\n\r" | "\n" | "\r"}
whitespace = _{" " | "\t"}
